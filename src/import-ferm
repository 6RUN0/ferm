#!/usr/bin/perl

#
# ferm, a firewall setup program that makes firewall rules easy!
#
# Copyright (C) 2001-2005 Auke Kok, Max Kellermann
#
# Comments, questions, greetings and additions to this program
# may be sent to <ferm@foo-projects.org>
#

# This tool allows you to import an existing firewall configuration
# into ferm.

#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#

# $Id$

use strict;

use Data::Dumper;

use vars qw($indent $table $chain @rules);

my %p1 = ( 'i' => 'interface',
           'o' => 'outerface',
           's' => 'saddr',
           'd' => 'daddr',
           'm' => 'mod',
           dport => 'dport',
           'reject-with' => 'reject-with',
           'limit' => 'limit',
           'icmp-type' => 'icmp-type',
           'to-destination' => 'to-destination', 
           'to-source' => 'to-source', 
         );

my %p1c = ( 'state' => 'state',
          );

sub is_netfilter_core_target {
    my $target = shift;
    die unless defined $target and length $target;

    return $target =~ /(?:ACCEPT|DROP|RETURN|QUEUE)$/;
}

sub is_netfilter_module_target {
    my $target = shift;
    die unless defined $target and length $target;

    return $target =~ /(?:BALANCE|CLASSIFY|CLUSTERIP|CONNMARK
                         |DNAT|DSCP|ECN|LOG|MARK|MASQUERADE
                         |MIRROR|NETMAP|REDIRECT|REJECT|ROUTE
                         |SNAT|TCPMSS|TOS|TRACE|TTL|ULOG
                         |TARPIT
                       )/x;
}

sub write_line {
    # write a line of tokens, with indent handling

    # don't add space before semicolon
    my $comma = $_[-1] eq ';' ? pop : '';
    # begins with closing curly braces -> decrease indent
    $indent -= 4 if $_[0] =~ /^}/;
    # do print line
    print ' ' x $indent;
    print join(' ', @_);
    print "$comma\n";
    # ends with opening curly braces -> increase indent
    $indent += 4 if $_[-1] =~ /{$/;
}

sub prefix_matches {
    my ($prefix, $rule) = @_;
    return unless exists $rule->{match};
    while (my ($key, $value) = each %$prefix) {
        return unless exists $rule->{match}{$key}
          and Dumper($rule->{match}{$key}) eq Dumper($value);
    }
    return 1;
}

sub prefix_match_count {
    my ($prefix, $rules) = @_;
    my $count = 0;
    foreach (@$rules) {
        last unless prefix_matches($prefix, $_);
        $count++;
    }
    return $count;
}

sub array_matches {
    my ($key, $rule1, $rule2) = @_;
    my %r1 = %$rule1;
    my %r2 = %$rule2;
    return unless exists $r1{match}{$key} and exists $r2{match}{$key};
    $r1{match} = {%{$r1{match}}};
    $r2{match} = {%{$r2{match}}};
    delete $r1{match}{$key};
    delete $r2{match}{$key};
    return Dumper(\%r1) eq Dumper(\%r2);
}

sub array_match_count {
    my ($key, $first, $rules) = @_;
    my $count = 0;
    foreach (@$rules) {
        last unless array_matches($key, $first, $_);
        $count++;
    }
    return $count;
}

sub optimize {
    my @result;

    # try to combine rules with arrays:
    # saddr 1.2.3.4 proto tcp ACCEPT;
    # saddr 5.6.7.8 proto tcp ACCEPT;
    # ->
    # saddr (1.2.3.4 5.6.7.8) proto tcp ACCEPT;
    while (@_ > 0) {
        my $rule = shift;
        if (exists $rule->{match}) {
            my $match_key;
            my $match_count = 0;
            my %match_copy = %{$rule->{match}};
            while (my ($key, $value) = each %match_copy) {
                next unless defined $value;
                my $match_count2 = array_match_count($key, $rule, \@_);
                if ($match_count2 > $match_count) {
                    $match_key = $key;
                    $match_count = $match_count2;
                }
            }

            if ($match_count > 0) {
                my @values = map {
                    my $value = $_->{match}{$match_key};
                    ref $value ? @$value : $value;
                } ($rule, splice(@_, 0, $match_count));
                $rule->{match}{$match_key} = '(' . join(' ', @values) . ')';
                unshift @_, $rule;
            } else {
                push @result, $rule;
            }
        } else {
            push @result, $rule;
        }
    }

    @_ = @result;
    undef @result;

    # try to find a common prefix and put rules in a block:
    # saddr 1.2.3.4 proto tcp dport ssh ACCEPT;
    # saddr 5.6.7.8 proto tcp dport ssh DROP;
    # ->
    # proto tcp dport ssh {
    #     saddr 1.2.3.4 ACCEPT;
    #     saddr 5.6.7.8 DROP;
    # }
    while (@_ > 0) {
        my $rule = shift;
        if (exists $rule->{match}) {
            my %prefix;
            my $match_count = 0;
            while (my ($key, $value) = each %{$rule->{match}}) {
                my $prefix2 = { $key => $value };
                my $match_count2 = prefix_match_count($prefix2, \@_);
                if ($match_count2 > $match_count) {
                    %prefix = %$prefix2;
                    $match_count = $match_count2;
                }
            }
            if ($match_count > 0) {
                my @block = map {
                    foreach my $key (keys %prefix) {
                        delete $_->{match}{$key};
                    }
                    #delete @_->{match}{keys %prefix};
                    $_;
                } ($rule, splice(@_, 0, $match_count));
                push @result, { match => \%prefix,
                                block => [ optimize(@block) ]
                              };
            } else {
                push @result, $rule;
            }
        } else {
            push @result, $rule;
        }
    }

    return @result;
}

sub flush {
    # optimize and write a list of rules

    my @r = @_ ? @_ : @rules;
    @r = optimize(@r);
    foreach my $rule (@r) {
        my @line;
        # assemble the line, match stuff first, then target parameters
        foreach (qw(match jump target)) {
            next unless exists $rule->{$_};
            while (my ($key, $value) = each %{$rule->{$_}}) {
                push @line, $key;
                push @line, ref $value ? @$value : $value
                  if defined $value;
            }
        }

        if (exists $rule->{block}) {
            # this rule begins a block created in &optimize
            write_line(@line, '{');
            flush(@{$rule->{block}});
            write_line('}');
        } else {
            # just a simple rule
            write_line(@line, ';');
        }
    }
    undef @rules;
}

print "# ferm rules generated by import-ferm\n";
print "# http://ferm.foo-projects.org/\n";
write_line qw(domain ip {);

while (<>) {
    chomp;
    if (/^(?:#.*|COMMIT)?$/) {
        # empty, comment or commit
    } elsif (/^\*(\w+)$/) {
        # table
        write_line('}') if defined $table;
        $table = $1;
        write_line('table', $table, '{');
    } elsif (/^:(\S+)\s+-\s+/) {
        # custom chain
        die unless defined $table;
        write_line("chain $1;");
    } elsif (/^:(\S+)\s+(\w+)\s+/) {
        # built-in chain
        die unless defined $table;
        write_line('chain', $1, 'policy', $2, ';');
    } elsif (s/^-A (\S+) //) {
        # a rule
        unless (defined $chain) {
            flush;
            $chain = $1;
            write_line('chain', $chain, '{');
        } elsif ($1 ne $chain) {
            flush;
            write_line('}');
            $chain = $1;
            write_line('chain', $chain, '{');
        }

        my %line;
        # separate 'match' parameters from 'targe't parameters; $cur
        # points to the current position
        my $cur = $line{match} = {};
        while (s/^-(\w) *// || s/^--(\S+) *//) {
            my $option = $1;
            if ($option eq 'p') {
                die unless s/^(\S+) *//;
                my $param = $1;
                # protocol implicitly loads the module
                delete $cur->{mod}
                  if exists $cur->{mod} and $cur->{mod} eq $param;
                $cur->{proto} = $param;
            } elsif ($option eq 'm') {
                die unless s/^(\S+) *//;
                my $param = $1;
                # we don't need this module if the protocol with the
                # same name is already specified
                $cur->{mod} = $param
                  unless exists $cur->{proto} and $cur->{proto} eq $param;
            } elsif (exists $p1{$option}) {
                die unless s/^(\S+) *//;
                my $param = $1;
                my $keyword = $p1{$option};
                $cur->{$keyword} = $param;
            } elsif (exists $p1c{$option}) {
                die unless s/^(\S+) *//;
                my $param = $1;
                my $keyword = $p1c{$option};
                my @params = split(',', $param);
                $cur->{$keyword} = @params == 1 ? $param : \@params;
            } elsif ($option eq 'j') {
                die unless s/^(\S+) *//;
                my $target = $1;
                # store the target in $line{jump}
                $cur = $line{jump} = {};
                unless (is_netfilter_core_target($target) ||
                        is_netfilter_module_target($target)) {
                    $cur->{goto} = $target;
                } else {
                    $cur->{$target} = undef;
                }
                # what now follows is target parameters; set $cur
                # correctly
                $cur = $line{target} = {};
            } else {
                print STDERR "option '$option' not understood, ignoring line $.\n";
                next;
            }

        }
        push @rules, \%line;
    } else {
        print STDERR "line $. was not understood, ignoring it\n";
    }
}

flush;
write_line '}' if defined $chain;
write_line '}' if defined $table;

write_line qw(});
